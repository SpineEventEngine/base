// Apply this script if it is needed to generate Protobuf descriptor set (special files with `.desc` extension).
// These files may be needed by Gradle plugins to obtain descriptors of `.proto` files of this project.

protobuf {
    generateProtoTasks {
        all().each { final task ->
            final def name = task.sourceSet.name
            final def descriptorPath = "$projectDir/build/descriptors/${name}.desc"
            task.plugins {
                task.generateDescriptorSet = true
                task.descriptorSetOptions.path = descriptorPath
                task.descriptorSetOptions.includeImports = true
            }
            task.doLast {
                // TODO:2018-05-21:dmytro.dashenkov: Delete this action after migrating on 0.10.38
                copy {
                    from descriptorPath
                    into "$buildDir/resources/${name}"
                }

                final def resultFile = "$buildDir/resources/$name/${name}.desc" as File

                logger.info("Copied descriptor into file $resultFile, ${resultFile.exists()}")
            }
        }
    }
}
