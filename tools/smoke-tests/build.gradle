/*
 * Copyright 2020, TeamDev. All rights reserved.
 *
 * Redistribution and use in source and/or binary forms, with or without
 * modification, must retain the above copyright notice and the following
 * disclaimer.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

buildscript {
    ext.baseRoot = "$rootDir/../.."
    ext.versionGradle = "$baseRoot/version.gradle.kts"


    apply from: versionGradle
    apply from: "$baseRoot/config/gradle/dependencies.gradle"

    repositories {
        maven { url = repos.gradlePlugins }
        jcenter()
        mavenLocal()

        maven { url = repos.oldSpine }
        maven { url = repos.oldSpineSnapshots }
        
        maven { url = repos.spine }
        maven { url = repos.spineSnapshots }
    }


    dependencies {
        classpath deps.build.guava
        classpath (deps.build.gradlePlugins.protobuf) {
            exclude group: 'com.google.guava'
        }
        classpath (deps.build.gradlePlugins.errorProne) {
            exclude group: 'com.google.guava'
        }
        // Always use the latest (current) version of model compiler no matter
        // the global configuration.
        classpath "io.spine.tools:spine-model-compiler:$spineVersion"
    }
}

allprojects {
    apply from: versionGradle
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'project-report'

    repositories {
        mavenLocal()
        jcenter()
    }

    ext.baseRoot = "$rootDir/../.."
}

subprojects {
    apply plugin: 'com.google.protobuf'
    apply plugin: 'io.spine.tools.spine-model-compiler'
    apply plugin: 'idea'
    apply from: "$baseRoot/config/gradle/test-output.gradle"
    apply from: "$baseRoot/config/gradle/model-compiler.gradle"

    dependencies {
        implementation(
                deps.build.errorProneCore,
                deps.build.errorProneAnnotations,
                "io.spine:spine-base:$spineVersion"
        )

        testImplementation "io.spine:spine-testlib:$spineVersion"
        testRuntimeOnly deps.test.junit5Runner
    }

    forceConfiguration(project)

    idea.module {
        generatedSourceDirs += file("$projectDir/generated/main/java")
        generatedSourceDirs += file("$projectDir/generated/main/spine")

        generatedSourceDirs += file("$projectDir/generated/test/java")
        generatedSourceDirs += file("$projectDir/generated/test/spine")
    }

    sourceSets {
        main {
            proto.srcDirs "$projectDir/src/main/proto"
            java.srcDirs "$projectDir/generated/main/java",
                         "$projectDir/generated/main/spine",
                         "$projectDir/src/main/java"
            resources.srcDirs "$projectDir/generated/main/resources"
        }

        test {
            proto.srcDirs "$projectDir/src/test/proto"
            java.srcDirs "$projectDir/generated/test/java",
                         "$projectDir/generated/test/spine",
                         "$projectDir/src/test/java"
            resources.srcDirs "$projectDir/generated/test/resources"
        }
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }

        include "**/*Test.class"
    }
}

apply from: "$baseRoot/config/gradle/jacoco.gradle"
