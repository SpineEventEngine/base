/*
 * Copyright 2018, TeamDev. All rights reserved.
 *
 * Redistribution and use in source and/or binary forms, with or without
 * modification, must retain the above copyright notice and the following
 * disclaimer.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

import org.apache.tools.ant.filters.ReplaceTokens

import java.nio.file.Files
import java.nio.file.StandardCopyOption

ext {
    roasterVersion = '2.20.1.Final'
    spineFolder = "${project.projectDir}/.spine" as File
    protocPluginDependency = null
}

group = 'io.spine.tools'

dependencies {
    implementation project(':plugin-base')
    implementation project(':base')

    implementation (group: 'com.google.template', name: 'soy', version: '2017-02-01') {
        exclude group: 'com.google.guava'
    }

    implementation deps.gen.javaPoet

    // A library for parsing Java sources.
    // Used for parsing Java sources generated from Protobuf files
    // to make their annotation more convenient.
    implementation (group: 'org.jboss.forge.roaster', name: 'roaster-api', version: roasterVersion) {
        exclude group: 'com.google.guava'
    }
    
    implementation (group: 'org.jboss.forge.roaster', name: 'roaster-jdt', version: roasterVersion) {
        exclude group: 'com.google.guava'
    }

    testImplementation project(':testlib')
    testImplementation deps.test.hamcrest
    testImplementation gradleTestKit()
    testImplementation project(':plugin-testlib')

    // The freshest version of the plugin required for tests
    protocPluginDependency = testCompileOnly "io.spine.tools:spine-protoc-plugin:$spineVersion@jar"
}

protobuf {
    generatedFilesBaseDir = generatedRootDir

    protoc {
        artifact = deps.build.protoc
    }
}

/**
 * Prepares the `spine-protoc.gradle` file by copying its template to the `resources` directory and
 * injecting the versions of the protoc plugin dependencies.
 *
 * <p>This task is executed each time `processResources` task is executed, i.e. upon each build.
 */
task prepareProtocConfig(type: Copy) {
    description = "Prepares the spine-protoc.gradle.template file"

    from "./spine-protoc.gradle.template"
    into "./src/main/resources/"

    rename { "./spine-protoc.gradle" }
    
    filter(ReplaceTokens,
            tokens: ['baseVersion': "'${spineVersion}'".toString(),
                     'pbVersion'  : "'${deps.versions.protobuf}'".toString(),
                     'gRPCVersion': "'${deps.versions.grpc}'".toString(),
                     // See `DefaultProject.tempArtifacts()`
                     'spineDir'   : "'.spine'"])
    outputs.upToDateWhen { false }
}

processResources.dependsOn prepareProtocConfig

final def copyPluginJarAction = {
    final def from = configurations.testCompileOnly.fileCollection(protocPluginDependency).singleFile
    final def srcPath = from.toPath()
    final def dest = project.spineFolder.toPath().resolve(from.name)
    dest.toFile().mkdirs()
    Files.copy(srcPath, dest, StandardCopyOption.REPLACE_EXISTING)
}

// We cannot use standard Copy task here as it resolves the `from` property not lazily.
// Since we use use a dependency in the `from`, it may cause some issues with the Maven plugin
// See https://discuss.gradle.org/t/right-way-to-copy-contents-from-dependency-archives/7449
task copyProtocPluginTestArtifact {
    description = "Spawns the Spine Protoc plugin artifact in the project directory for tests"
}

copyProtocPluginTestArtifact.doLast(copyPluginJarAction)
copyProtocPluginTestArtifact.dependsOn project.project(':protoc-plugin').publishToMavenLocal

// Tests use the Protobuf plugin.
test {
    dependsOn copyProtocPluginTestArtifact
    dependsOn rootProject.subprojects*.publishToMavenLocal
}
