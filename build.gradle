/*
 * Copyright 2019, TeamDev. All rights reserved.
 *
 * Redistribution and use in source and/or binary forms, with or without
 * modification, must retain the above copyright notice and the following
 * disclaimer.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */
buildscript { final scriptHandler ->

    // As long as `buildscript` section is always evaluated first,
    // we need to apply explicitly here.
    apply from: "$rootDir/config/gradle/dependencies.gradle"
    apply from: "$rootDir/version.gradle"

    defaultRepositories(scriptHandler)
    dependencies {
        classpath deps.build.guava
        classpath deps.build.gradlePlugins.protobuf
        classpath deps.build.gradlePlugins.errorProne
    }

    forceConfiguration(scriptHandler)
}

apply from: 'version.gradle'

ext {
    groupId = 'io.spine'
    credentialsPropertyFile = 'credentials.properties'

    projectsToPublish = [
        'base',
        'tool-base',
        'testlib',
        'mute-logging',
        'errorprone-checks',

        // Gradle plugins
        'plugin-base',
        'javadoc-filter',
        'javadoc-prettifier',
        'proto-dart-plugin',
        'proto-js-plugin',
        'model-compiler',

        'plugin-testlib',

        // Protoc compiler plugin
        'protoc-api',
        'validation-generator',
        'protoc-plugin'
    ]
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'jacoco'
    apply plugin: 'idea'
    apply plugin: 'project-report'

    // Use the same version numbering for the Spine Base library.
    version = versionToPublish
}

subprojects {
    buildscript { final scriptHandler ->

        apply from: "$rootDir/version.gradle"

        defaultRepositories(scriptHandler)
        dependencies {
            classpath deps.build.guava
            classpath deps.build.gradlePlugins.protobuf
        }

        forceConfiguration(scriptHandler)
    }

    project.ext {
        spineProtobufPluginId = 'io.spine.tools.spine-model-compiler'

        sourcesRootDir = "$projectDir/src"
        generatedRootDir = "$projectDir/generated"

        generatedJavaDir = "$generatedRootDir/main/java"
        generatedTestJavaDir = "$generatedRootDir/test/java"

        generatedGrpcDir = "$generatedRootDir/main/grpc"
        generatedTestGrpcDir = "$generatedRootDir/test/grpc"

        generatedSpineDir = "$generatedRootDir/main/spine"
        generatedTestSpineDir = "$generatedRootDir/test/spine"

        runsOnWindows = org.gradle.internal.os.OperatingSystem.current().isWindows()
    }

    apply plugin: 'java-library'
    apply plugin: 'pmd'
    apply plugin: 'com.google.protobuf'
    apply plugin: 'net.ltgt.errorprone'

    apply plugin: 'maven-publish'

    apply from: deps.scripts.projectLicenseReport

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    defaultRepositories(project)
    dependencies {
        errorprone deps.build.errorProneCore
        errorproneJavac deps.build.errorProneJavac
        // For dependencies config. based on version of Java, see:
        //  https://github.com/epeee/junit-jupiter-extension-testing/blob/57b7ba75ab64ed8c229d2a5b14a954d6ae359189/gradle/errorprone.gradle
        
        api deps.build.protobuf
        api deps.build.flogger

        implementation deps.build.guava
        implementation deps.build.checkerAnnotations
        implementation deps.build.errorProneAnnotations
        implementation deps.build.jsr305Annotations

        testImplementation deps.test.guavaTestlib
        testImplementation deps.test.junit5Api
        testImplementation deps.test.junit5Runner
        testImplementation deps.test.junitPioneer

        runtime deps.runtime.floggerSystemBackend
    }

    forceConfiguration(project)
    configurations {
        // Avoid collisions of Java classes defined both in `protobuf-lite` and `protobuf-java`
        runtime.exclude group: "com.google.protobuf", module: "protobuf-lite"
        testRuntime.exclude group: "com.google.protobuf", module: "protobuf-lite"
    }

    sourceSets {
        main {
            java.srcDirs generatedJavaDir, "$sourcesRootDir/main/java", generatedSpineDir
            resources.srcDirs "$sourcesRootDir/main/resources", "$generatedRootDir/main/resources"
        }
        test {
            java.srcDirs generatedTestJavaDir, "$sourcesRootDir/test/java", generatedTestSpineDir
            resources.srcDirs "$sourcesRootDir/test/resources", "$generatedRootDir/test/resources"
        }
    }

    protobuf {
        generatedFilesBaseDir = generatedRootDir

        protoc {
            artifact = deps.build.protoc
        }
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }

        include "**/*Test.class"
    }

    apply from: deps.scripts.testOutput
    apply from: deps.scripts.javadocOptions
    apply from: deps.scripts.javacArgs
    
    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier.set("sources")
    }

    task testOutputJar(type: Jar) {
        from sourceSets.test.output
        archiveClassifier.set("test")
    }

    task javadocJar(type: Jar, dependsOn: 'javadoc') {
        from "$projectDir/build/docs/javadoc"
        archiveClassifier.set("javadoc")
    }

    // Apply the same IDEA module configuration for each of sub-projects.
    idea {
        module {
            generatedSourceDirs += file(generatedJavaDir)
            testSourceDirs += file(generatedTestJavaDir)
            downloadJavadoc = true
            downloadSources = true

            iml {
                beforeMerged { final module ->
                    module.dependencies.clear()
                }
                whenMerged { final module ->
                    module.dependencies*.exported = true
                }
            }
        }
    }

    task cleanGenerated(type: Delete) {
        delete = "$projectDir/generated"
    }
    
    clean.dependsOn cleanGenerated

    apply from: deps.scripts.pmd
}

// IDEA project configuration.
idea {
    project {
        ipr {
            beforeMerged { final project ->
                project.modulePaths.clear()
            }
            withXml { final provider ->
                provider.node.component
                        .find { it.@name == 'VcsDirectoryMappings' }
                        .mapping.@vcs = 'Git'
            }
        }
    }
}

apply from: deps.scripts.jacoco
apply from: deps.scripts.publish
apply from: deps.scripts.runBuild
apply from: deps.scripts.generatePom

apply from: deps.scripts.repoLicenseReport

task smokeTests  {
    doLast {
        runBuild("$rootDir/tools/smoke-tests")
    }
}

task buildAll {
    dependsOn build, smokeTests
}
