// Common build file for the tests with same configuration

buildscript {

    // Applying from `ext.gradle` inside the `buildscript` section to reuse the properties.
    //
    // As long as `buildscript` section is always evaluated first, we need to apply
    // `ext.gradle` explicitly here.
    //
    // NOTE: this file is copied from the root project in the test setup.
    apply from: 'ext.gradle'

    repositories {
        mavenLocal()
        mavenCentral()

        maven { url = 'http://maven.teamdev.com/repository/spine' }
        maven { url = 'http://maven.teamdev.com/repository/spine-snapshots' }
    }

    dependencies {
        classpath "com.google.protobuf:protobuf-java:${protobufVersion}"
        classpath "com.google.protobuf:protobuf-gradle-plugin:${protobufGradlePluginVerison}"
        classpath "io.spine.tools:spine-model-compiler:${spineVersion}"
    }

    configurations.all({
        resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
    })
}

apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'io.spine.tools.spine-model-compiler'

// NOTE: this file is copied from the root project in the test setup.
apply from: 'ext.gradle'


repositories {
    mavenLocal()
    mavenCentral()

    maven { url = 'http://maven.teamdev.com/repository/spine' }
    maven { url = 'http://maven.teamdev.com/repository/spine-snapshots' }
}

compileJava {
    options.compilerArgs += ["-processor", "io.spine.tools.model.AssignLookup", "-AspineDirRoot=${rootDir}"]
}

dependencies {

    compile group: 'com.google.protobuf', name:'protobuf-java', version: protobufVersion

    compile(group: 'io.spine', name: 'spine-base', version: spineVersion) {
        exclude(group: 'com.google.protobuf')
    }

    compileOnly group: 'io.spine.tools', name: 'model-assembler', version: spineToolsVersion

}

sourceSets {
    main {
        proto.srcDirs += ["$projectDir/src/main/proto"]
        java.srcDirs += ["$projectDir/generated/main/java",
                        "$projectDir/generated/main/spine"]
        resources.srcDirs += ["$projectDir/generated/main/resources"]
    }
}
