// Common build file for the tests with same configuration

buildscript {

    // Applying from `ext.gradle` inside the `buildscript` section to reuse the properties.
    //
    // As long as `buildscript` section is always evaluated first, we need to apply
    // `ext.gradle` explicitly here.
    //
    // NOTE: this file is copied from the root project in the test setup.
    apply from: 'ext.gradle'

    repositories {
        mavenLocal()
        mavenCentral()

        maven { url = 'http://maven.teamdev.com/repository/spine' }
        maven { url = 'http://maven.teamdev.com/repository/spine-snapshots' }
    }

    dependencies {
        classpath "com.google.protobuf:protobuf-java:${protobufVersion}"
        classpath "com.google.protobuf:protobuf-gradle-plugin:${protobufGradlePluginVerison}"
        classpath "io.spine.tools:spine-model-compiler:${spineVersion}"
    }

    configurations.all({
        resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
    })
}

apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'io.spine.tools.spine-model-compiler'

// NOTE: this file is copied from the root project in the test setup.
apply from: 'ext.gradle'


repositories {
    mavenLocal()
    mavenCentral()

    maven { url = 'http://maven.teamdev.com/repository/spine' }
    maven { url = 'http://maven.teamdev.com/repository/spine-snapshots' }
}

dependencies {

    compile "com.google.protobuf:protobuf-java:${protobufVersion}"

    //TODO:6/20/17:alex.tymchenko: switch to `base` dependency instead.
    compile(group: 'io.spine', name: 'spine-client-core', version: spineVersion) {
        exclude(group: 'com.google.protobuf')
    }

}

protobuf {
    generatedFilesBaseDir = "$projectDir/generated"
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${gRpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { final task ->
            task.generateDescriptorSet = true
            task.descriptorSetOptions.path = "${projectDir}/build/descriptors/${task.sourceSet.name}.desc"
            task.descriptorSetOptions.includeImports = true
            task.descriptorSetOptions.includeSourceInfo = true
            task.plugins {
                grpc {}
            }
        }
    }
}

sourceSets {
    main {
        proto.srcDirs = ["$projectDir/src/main/proto"]
        java.srcDirs = ["$projectDir/generated/main/java",
                        "$projectDir/generated/main/spine",
                        "$projectDir/src/main/java"]
        resources.srcDirs = ["$projectDir/generated/main/resources"]
    }
}
