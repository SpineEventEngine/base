//
// Copyright 2017, TeamDev Ltd. All rights reserved.
//
// Redistribution and use in source and/or binary forms, with or without
// modification, must retain the above copyright notice and the following
// disclaimer.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
//
buildscript {
    apply from: "$rootDir/ext.gradle"
    
    repositories {
        jcenter()
        maven { url = googleMavenCentralMirror }

        mavenCentral()
        mavenLocal()

        // Spine releases repository.
        maven { url = spineRepository }

        // Spine snapshots repository.
        maven { url = spineSnapshotsRepository }
    }

    //noinspection GroovyAssignabilityCheck
    dependencies {
        classpath group: 'com.google.guava', name: 'guava', version: guavaVersion
        classpath(group: 'com.google.protobuf',
                  name: 'protobuf-gradle-plugin',
                  version: protobufGradlePluginVerison) {
            // exclude an old Guava version
            exclude group: 'com.google.guava'
        }
        classpath group: 'io.spine.tools', name: 'spine-model-compiler', version: spineVersion
    }
    
    configurations.all({
        resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
    })
}

configurations {
    // Avoid collisions of Java classes defined both in `protobuf-lite` and `protobuf-java`
    runtime.exclude group: "com.google.protobuf", module: "protobuf-lite"
    testRuntime.exclude group: "com.google.protobuf", module: "protobuf-lite"
}

apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: spineProtobufPluginId

apply from: generateDescriptorSetScript

apply from: testArtifactsScript

dependencies {
    /* As a Library, we provide logging facade API, not specific logger bindings.
       Target apps are free to use any binding they need. */
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4JVersion

    compile group: 'com.google.protobuf', name: 'protobuf-java', version: protobufVersion
    compile group: 'com.google.protobuf', name: 'protobuf-java-util', version: protobufVersion

    // Use jdk14 bindings for test purposes only.
    testCompile group: 'org.slf4j', name: 'slf4j-jdk14', version: slf4JVersion
    testCompile(group: 'junit', name: 'junit', version: jUnitVersion) {
        exclude(module: 'hamcrest-core')
    }
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: hamcrestVersion
    testCompile group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
    testCompile group: 'com.google.guava', name: 'guava-testlib', version: guavaVersion

    testCompile project(path: ":testutil-base")
}

protobuf {
    generatedFilesBaseDir = generatedRootDir
    protoc {
        artifact = protobufDependency
    }

    /* The below code removes deprecation annotations from the code generated by
       Protobuf compiler. See details here: https://github.com/google/protobuf/issues/2054 */
    generateProtoTasks {
        all().each { final task ->
            task.descriptorSetOptions.includeSourceInfo = true
            task.doLast {
                ant.replaceregexp(
                        match:'@java.lang.Deprecated|@deprecated',
                        replace:'',
                        flags:'g',
                        byline:true
                ) {
                    fileset(dir: "$protobuf.generatedFilesBaseDir/main/java")

                    final generatedTests = "$protobuf.generatedFilesBaseDir/test/java"
                    if (new File(generatedTests).exists()) {
                        fileset(dir: generatedTests)
                    }
                }
            }
        }
    }
}

