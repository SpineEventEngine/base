/*
 * Copyright 2018, TeamDev. All rights reserved.
 *
 * Redistribution and use in source and/or binary forms, with or without
 * modification, must retain the above copyright notice and the following
 * disclaimer.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

group = 'io.spine'

configurations {
    // Avoid collisions of Java classes defined both in `protobuf-lite` and `protobuf-java`
    runtime.exclude group: "com.google.protobuf", module: "protobuf-lite"
    testRuntime.exclude group: "com.google.protobuf", module: "protobuf-lite"
}

apply plugin: 'java'
apply plugin: 'com.google.protobuf'

apply from: deps.scripts.testArtifacts

dependencies {
    // Re-generate standard Proto types, so they are included in `known_types.desc`.
    //noinspection GroovyAssignabilityCheck
    protobuf deps.build.protobuf

    // Since we use `@CanIgnoreReturnValue` when annotating Validating Builders
    // ErrorProne Annotations is part of our API. 
    api deps.build.errorProneAnnotations
    
    testImplementation project(path: ":testlib")
}

protobuf {
    generatedFilesBaseDir = "$projectDir/generated"
    protoc {
        artifact = deps.build.protoc
    }

    generateProtoTasks {
        all().each { final task ->
            task.generateDescriptorSet = true
            task.descriptorSetOptions.path = "$buildDir/descriptors/${task.sourceSet.name}/known_types.desc"
            task.descriptorSetOptions.includeImports = true
            task.descriptorSetOptions.includeSourceInfo = true
        }
    }
}

sourceSets {
    main {
        resources.srcDirs += "$buildDir/descriptors/main"
        proto.srcDirs = ["$projectDir/src/main/proto"]
    }
    test {
        resources.srcDirs += "$buildDir/descriptors/test"
        proto.srcDirs = ["$projectDir/src/test/proto"]
    }

}

jar {
    // See `base-validating-builders/README.md`.
    from "$rootDir/base-validating-builders/builders"
}

build.doLast {
    final boolean shouldClean = gradle.getTaskGraph().hasTask('clean')
    final def directory = "$rootDir/base-validating-builders"
    final def command = []
    final def script = runsOnWindows ? "gradlew.bat" : "gradlew"
    command.add("$directory/../$script".toString())
    if (shouldClean) {
        command.add('clean')
    }
    command.add('build')
    command.add('--console=plain')
    command.add('--debug')

    // Ensure build error output log. 
    // Since we're executing this task in another process, we redirect error output to 
    // the file under the `build` directory.  
    final File buildDir = new File(directory, "build")
    if (!buildDir.exists()) {
        buildDir.mkdir()
    }
    final File errorOut = new File(buildDir, 'error-out.txt')
    final File debugOut = new File(buildDir, 'debug-out.txt')

    final def process = new ProcessBuilder()
            .command(command)
            .directory(file(directory))
            .redirectError(errorOut)
            .redirectOutput(debugOut)
            .start()
    if (process.waitFor() != 0) {
        throw new GradleException("Unable to build validating builders. See $errorOut for details.")
    }
}
